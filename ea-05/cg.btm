RULE begin cg
CLASS EscapeAnalysisBasicBlockBuilder
METHOD <init>
AT EXIT
BIND
  method = $2.getCurrentElement().toString().replaceAll("[\\.\\[(/;)]", "_")
IF
  $2.getCurrentElement().toString().startsWith("example")
DO
  traceOpen($2.getCurrentElement(), method + ".dot");
  trace($2.getCurrentElement(), "digraph " + method + " {\n");
  trace($2.getCurrentElement(), "node [colorscheme=pastel23]\n");
ENDRULE

RULE cg field edge if absent
CLASS EscapeAnalysis$ConnectionGraph
METHOD addFieldEdgeIfAbsent
AT EXIT
BIND
  obj = $1.toString().replaceAll("[\\.@]", "_");
  field = $2.toString().replaceAll("[\\.@]", "_");
  success = $!;
IF
  $this.element.toString().startsWith("example")
  AND
  success
DO
  trace($this.element, obj + " -> " + field + " [label = \"F\"]\n");
ENDRULE

RULE cg points-to edge if absent
CLASS EscapeAnalysis$ConnectionGraph
METHOD addPointsToEdgeIfAbsent
AT EXIT
BIND
  ref = $1.toString().replaceAll("[\\.@]", "_");
  obj = $2.toString().replaceAll("[\\.@]", "_");
  success = $!;
IF
  $this.element.toString().startsWith("example")
  AND
  success
DO
  trace($this.element, ref + " -> " + obj + " [label = \"P\"]\n");
ENDRULE

RULE cg deferred edge if absent
CLASS EscapeAnalysis$ConnectionGraph
METHOD addDeferredEdgeIfAbsent
AT EXIT
BIND
  ref = $1.toString().replaceAll("[\\.@]", "_");
  obj = $2.toString().replaceAll("[\\.@]", "_");
  success = $!;
IF
  $this.element.toString().startsWith("example")
  AND
  success
DO
  trace($this.element, ref + " -> " + obj + " [style=dashed label = \"D\"]\n");
ENDRULE

RULE close cg graph
CLASS EscapeAnalysis$ConnectionGraph
METHOD methodExit
AT EXIT
IF
  $this.element.toString().startsWith("example")
DO
  trace($this.element, "\"Global Escape\" [style=filled fillcolor = 2]\n");
  trace($this.element, "\"Arg Escape\" [style=filled fillcolor = 3]\n");
  trace($this.element, "\"No Escape\" [style=filled fillcolor = 1]\n");
  trace($this.element, "\"Unknown\" [style=filled fillcolor = white]\n");
  trace($this.element, "}\n");
ENDRULE

RULE cg set arg escape
CLASS EscapeAnalysis$ConnectionGraph
METHOD setArgEscape
AT ENTRY
BIND
  node = $1.toString().replaceAll("[\\.@]", "_");
IF
  $this.element.toString().startsWith("example")
DO trace($this.element, node + " [style=filled, fillcolor = 3]\n")
ENDRULE

RULE cg set global escape
CLASS EscapeAnalysis$ConnectionGraph
METHOD setGlobalEscape
AT ENTRY
BIND
  node = $1.toString().replaceAll("[\\.@]", "_");
IF
  $this.element.toString().startsWith("example")
DO trace($this.element, node + " [style=filled, fillcolor = 2]\n")
ENDRULE

RULE cg set no escape
CLASS EscapeAnalysis
METHOD setNoEscape
AT ENTRY
BIND
  node = $1.toString().replaceAll("[\\.@]", "_");
IF
  $this.element.toString().startsWith("example")
DO trace($this.element, node + " [style=filled, fillcolor = 1]\n")
ENDRULE
